; XpkMaster distribution Installer script.
; © Dirk Stöcker <stoecker@amigaworld.com>
; $VER: XpkMasterInstall 1.25 (26.12.1999)

; History:
; 1.10	changed WHAT_C
; 1.11	added Russian language and install check for "nederlands"
; 1.12  added install check for "español"
; 1.13  finally fixed Installer version error report
; 1.14  added new languages
; 1.15  added Finish language
; 1.16  added question for language
; 1.17  fixed xpk_Develop installation
; 1.18  fixed Serbian texts, added Crotian and Spain language
; 1.19  added Dutch language
; 1.20  little bugfix
; 1.21  bugfixes
; 1.22  bugfixes
; 1.23  bug fix in Danish text
; 1.24  added català language checks
; 1.25  little bug fix, removed average and expert keywords from confirm to
;       work around OS3.5 installer bug

; Translations, which equal english texts are not needed, as not used texts
; get english defaults!

(set #catalog 16) ; english language is default

(set #i 0)
(while (set #thisfile (select #i
		"català" "dansk" "deutsch" "español" "français" "hrvatski"
		"italiano" "nederlands" "norsk" "polski" "português"
		"russian" "srpski" "suomi" "Svenska" "ÃeÓtina"
  ""))
  (
    (if (= @language #thisfile) (set #catalog #i))
    (set #i (+ #i 1))
  )
)

(set #catalog
  (askchoice
    (prompt "Select your native language")
    (help @askoptions-help)
    (choices
		"" "dansk" "deutsch" "español" "français" "hrvatski"
		"italiano" "nederlands" "" "polski" ""
		"russian" "srpski" "suomi" "Svenska" "ÃeÓtina"
    "ENGLISH")
    (default #catalog)
  )
)

; ************************** English texts ******************************

(set #GOINGIN
  (cat "\n\nWelcome to XpkMaster distribution installation system.\n\n"
       "All sub archives you want to install have to be unpacked to "
       "one directory. This directory is the parent of the one, you "
       "have started the script of!\n\nMake clear you did this!"
  )
)
(set #INSTALLERVERSION "You need at least version 42.9 of Installer.")
(set #XPKMASTER_LIB  "Update xpkmaster.library when needed.")
(set #INSTALL_LIB    "Update sub library %s.")
(set #WHAT_STUFF     "What programs do you like to install ?")
(set #WHAT_LANGUAGE  "What language files do you like to install?")
(set #WHAT_PROMPT    "What parts of the distribution do you like to install?")
(set #WHAT_LIBS      "What sub libraries do you like to install?")
(set #WHAT_DOCS      "\n\n\n\n\nInstall documentation?")
(set #WHAT_PREFS     "\n\n\n\n\nInstall Preferences program?")
(set #WHAT_PROCESSOR "\n\n\n\n\nInstall MC68020 versions of some libraries?")
(set #WHAT_DEVEL     "What parts of developer dir do you need?")
(set #WHAT_C
  (cat "Choose the parts you need for your C compiler. The files are "
        "automatically copied into correct sub directories."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\nInstall cryption lib sources?")
(set #STARTUP	     "\n\n\n\n\nAdd XpkMasterPrefs call to startup file.")
(set #PREFSFILE	     "\n\n\n\n\nInstall preferences file?")
(set #ASK_PATH
  (cat "Select the path where %s should be installed. A drawer "
       "»%s« will be created automatically, when not existing."
  )
)
(set #ASK_PATH2     "Select the path where %s should be installed.")
(set #WHAT_USER     "User part")
(set #WHAT_DEVELOP  "Developer part")
(set #WHAT_CRYPT    "cryption libraries")
(set #WHAT_SOURCE   "sources and examples")
(set #DEV_AUTODOCS  "Autodocs")
(set #DEV_HOTHELP   "HotHelp files")
(set #DEV_FD	    "FD files")
(set #DEV_ASM	    "ASM includes")
(set #DEV_C	    "C/C++ includes")
(set #DEVT_AUTODOCS "the autodocs")
(set #DEVT_FD_FILES "the FD files")
(set #DEVT_ASM      "the ASM includes")
(set #DEVT_C	    "the C/C++ includes")
(set #DEVT_HOTHELP  "the HotHelp files")
(set #CRP_SOURCES   "the sources")
(set #SRC_SRC_A_EXA "the sources and examples")
(set #DEVC_PRAGMA   "pragma files")
(set #DEVC_GCC      "GCC inline files")
(set #DEVC_REDIRECT "redirect files for older code")
(set #DEVC_SAS      "SAS pragmas redirect (not needed)")
(set #DEVC_PROTO    "proto files")

; **************************** Danish texts *****************************

(if (= #catalog 1) (
(set #GOINGIN
  (cat "\n\nVelkommen til XpkMaster distribution installations-systemet.\n\n"
       "Alle sub-arkiver du vil installere skal udpakkes til en skuffe. "
       "Denne skuffe er forældre til den du har startet dette script fra! "
       "\n\nVær sikker på du har gjort dette!"
  )
)
(set #XPKMASTER_LIB  "Opdatere xpkmaster.library når nødvendigt.")
(set #INSTALL_LIB    "Opdatere sub-bibliotek %s.")
(set #WHAT_STUFF     "Hvilke programmer vil du gerne installere?")
(set #WHAT_LANGUAGE  "Hvilke sprogfiler vil du gerne installere?")
(set #WHAT_PROCESSOR "\n\n\n\n\nInstallere MC68020-versioner af nogle af bibliotekerne?")
(set #CRYPT_SOURCE   "\n\n\n\n\nInstallere krypteringsbiblioteks kildekode?")
(set #STARTUP     "\n\n\n\n\nTilføj XpkMasterPrefs kald til opstartsfil.")
(set #WHAT_C
  (cat "Vælg stien hvor %s skal installeres. En skuffe "
       "»%s« vil blive oprettet automatisk, hvis den ikke eksisterer."
  )
)
(set #WHAT_USER     "Brugerdel")
(set #WHAT_DEVELOP  "Udviklerdel")
(set #WHAT_CRYPT    "Krypterings-biblioteker")
(set #DEV_HOTHELP   "HotHelp-filer")
))
; ************************** German texts *******************************

(if (= #catalog 2) (
(set #GOINGIN
  (cat "\n\nWillkommen zum XpkMaster Installationssystem.\n\n"
       "Alle Teilarchive, die Sie installieren wollen, müssen in ein "
       "Verzeichnis entpackt werden. Dieses ist das vorhergehende "
       "Verzeichnis zu dem, aus dem Sie dieses Script gestartet haben.\n\n"
       "Stellen Sie sicher, das richtig getan zu haben!"
  )
)
(set #INSTALLERVERSION "Sie benötigen mindestens Version 42.9 des Installer Programmes.")
(set #XPKMASTER_LIB  "Erneuere xpkmaster.library wenn nötig.")
(set #INSTALL_LIB    "Erneuere Sublibrary %s.")
(set #WHAT_STUFF     "Welche Programme wollen Sie installieren?")
(set #WHAT_LANGUAGE  "Welche Sprachkataloge wollen Sie installieren?")
(set #WHAT_PROMPT    "Welche Teile von XPK wollen Sie installieren?")
(set #WHAT_LIBS      "Welche Sublibraries wollen Sie installieren?")
(set #WHAT_DOCS      "\n\n\n\n\nDokumentation installieren?")
(set #WHAT_PREFS     "\n\n\n\n\nVoreinsteller (Preferences) Programm installieren?")
(set #WHAT_PROCESSOR "\n\n\n\n\nMC68020 Versionen einiger Libraries installieren?")
(set #WHAT_DEVEL     "Welche Teile des Entwickler Pakets benötigen Sie?")
(set #WHAT_C
  (cat "Wählen Sie die Teile, die Sie für Ihren C Compiler benötigen. Die"
       "Dateien werden automatisch in die nötigen Unterverzeichnisse verteilt."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\nQuellkode der Verschlüsselungslibraries installieren?")
(set #STARTUP	     "\n\n\n\n\nErgänze XpkMasterPrefs Programmaufruf zur Startupdatei.")
(set #PREFSFILE	     "\n\n\n\n\nInstalliere Voreinsteller (Preferences) Datei?")
(set #ASK_PATH
  (cat "Wählen Sie den Pfad, wo %s installiert werden soll. Ein Verzeichnis "
       "»%s« wird automatisch erstellt, falls es noch nicht existiert."
  )
)
(set #ASK_PATH2     "Wählen Sie den Pfad, wo %s installiert werden soll.")
(set #WHAT_USER     "Anwender Archiv")
(set #WHAT_DEVELOP  "Entwickler Archiv")
(set #WHAT_CRYPT    "Verschlüsselungslibraries")
(set #WHAT_SOURCE   "Quellkodes und Beispiele")
(set #DEV_HOTHELP   "HotHelp-Dateien")
(set #DEV_FD	    "FD-Dateien")
(set #DEV_ASM	    "ASM Includes")
(set #DEV_C	    "C/C++ Includes")
(set #DEVT_AUTODOCS "die Autodocs")
(set #DEVT_FD_FILES "die FD-Dateien")
(set #DEVT_ASM      "die ASM Includes")
(set #DEVT_C	    "die C/C++ Includes")
(set #DEVT_HOTHELP  "die HotHelp-Dateien")
(set #CRP_SOURCES   "die Quellkodes")
(set #SRC_SRC_A_EXA "die Quellkodes und Beispiele")
(set #DEVC_PRAGMA   "pragma Dateien")
(set #DEVC_GCC      "GCC inline Dateien")
(set #DEVC_REDIRECT "Umleitungsdateien für älteren Kode")
(set #DEVC_SAS      "SAS pragmas Umleitungsdateien (nicht benötigt)")
(set #DEVC_PROTO    "proto Dateien")
))

; ************************** Spanish texts ******************************

(if (= #catalog 3) (
(set #GOINGIN
  (cat "\n\nBienvenido al guión instalador del paquete XpkMaster.\n\n"
       "Todos los elementos de desee instalar serán desempaquetados "
       "en un único directorio ¡que es el directorio padre de aquel en "
       "el que ha puesto en marcha el guión instalador!\n\n¡Tenga "
       "siempre esto en cuenta!"
  )
)
(set #INSTALLERVERSION "Este guión instalador necesita al menos Installer en su versión 42.9.")
(set #XPKMASTER_LIB  "Actualizar 'xpkmaster.library' si es necesario.")
(set #INSTALL_LIB    "Actualizar sub-biblioteca %s.")
(set #WHAT_STUFF     "¿Qué programa desea instalar?")
(set #WHAT_LANGUAGE  "¿Qué idiomas desea instalar?")
(set #WHAT_PROMPT    "¿Qué secciones del paquete de distribución desea instalar?")
(set #WHAT_LIBS      "¿Qué sub-bibliotecas desea instalar?")
(set #WHAT_DOCS      "\n\n\n\n\n¿Desea instalar la documentación?")
(set #WHAT_PREFS     "\n\n\n\n\n¿Desea instalar el programa de preferencias?")
(set #WHAT_PROCESSOR "\n\n\n\n\n¿Desea instalar las versiones MC68020 de las bibliotecas?")
(set #WHAT_DEVEL     "¿Qué secciones del directorio de programadores necesita?")
(set #WHAT_C
  (cat "Elija las secciones que necesite para su compilador C. Los ficheros serán "
        "copiado automáticamente en los subdirectorios adecuados."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\n¿Desea instalar los ficheros fuente de las sub-bibliotecas de encriptación?")
(set #STARTUP	     "\n\n\n\n\nAñadir la ejecución de XpkMasterPrefs en el fichero de arranque.")
(set #PREFSFILE	     "\n\n\n\n\n¿Desea instalar el fichero de preferencias?")
(set #ASK_PATH
  (cat "Elija el camino de acceso al lugar donde %s debe ser instalado. "
       "Un cajón »%s« se creará automáticamente, cuando no exista previamente."
  )
)
(set #ASK_PATH2     "Elija el camino de acceso al lugar donde %s debe ser instalado.")
(set #WHAT_USER     "Sección para usuarios")
(set #WHAT_DEVELOP  "Sección para programadores")
(set #WHAT_CRYPT    "Sub-bibliotecas de encriptación")
(set #WHAT_SOURCE   "Ficheros fuente y ejemplos")
(set #DEV_AUTODOCS  "documentación 'Autodoc'")
(set #DEV_HOTHELP   "ficheros HotHelp")
(set #DEV_FD	    "ficheros FD")
(set #DEV_ASM	    "ficheros 'include' para ensamblador")
(set #DEV_C	    "ficheros 'include' para C/C++")
(set #DEVT_AUTODOCS "documentación 'AutoDoc'")
(set #DEVT_FD_FILES "los ficheros FD")
(set #DEVT_ASM      "los ficheros 'include' para ensamblador")
(set #DEVT_C	    "ficheros 'include' para C/C++")
(set #DEVT_HOTHELP  "ficheros de ayuda HotHelp")
(set #CRP_SOURCES   "ficheros fuente")
(set #SRC_SRC_A_EXA "los ficheros fuente y ejemplos")
(set #DEVC_PRAGMA   "ficheros pragma")
(set #DEVC_GCC      "ficheros 'GCC inline'")
(set #DEVC_REDIRECT "ficheros de redirección para código antiguo")
(set #DEVC_SAS      "ficheros pragma SAS de redirección (no necesarios)")
(set #DEVC_PROTO    "ficheros de prototipos")
))

; ************************** French texts *******************************

(if (= #catalog 4) (
(set #GOINGIN
  (cat "\n\nBienvenue dans le système d'installation de xpkMaster.\n\n"
       "Toutes les sous archives que vous voulez installer doivent être "
       "décompressées dans un répertoire. Ce répertoire est le parent de "
       "celui duquel vous avez lancé ce script !\n\n"
       "Assurez vous que vous ayez fait cela !"
  )
)
(set #INSTALLERVERSION "Vous avez besoin de Installer version 42.9 au minimum.")
(set #XPKMASTER_LIB  "Mise à jour de xpkmaster.library lorsque c'est nécessaire.")
(set #INSTALL_LIB    "Mise à jour de la sous bibliothèque %s.")
(set #WHAT_STUFF     "Quels programmes voulez vous installer ?")
(set #WHAT_LANGUAGE  "Quelles fichiers de langue voulez vous installer ?")
(set #WHAT_PROMPT    "Quelles parties de la distribution voulez vous installer ?")
(set #WHAT_LIBS      "Quelles sous bibliothèques voulez vous installer ?")
(set #WHAT_DOCS      "\n\n\n\n\nInstaller la documentation ?")
(set #WHAT_PREFS     "\n\n\n\n\nInstaller le programme de préférences ?")
(set #WHAT_PROCESSOR "\n\n\n\n\nInstaller la version MC68020 de quelques bibliothèques ?")
(set #WHAT_DEVEL     "Quelles parties du répertoire développeur voulez vous ?")
(set #WHAT_C	     "Choisissez les parties nécessaires pour votre compilateur C.")
(set #CRYPT_SOURCE   "\n\n\n\n\nInstaller les sources des bibliothèques de cryptage ?")
(set #STARTUP	     "\n\n\n\n\nAjouter l'appel XpkMasterPrefs au fichier de démarrage.")
(set #ASK_PATH
  (cat "Sélectionnez le chemin où %s devrait être installé. Un répertoire "
       "« %s » sera créé automatiquement, s'il n'existe pas."
  )
)
(set #ASK_PATH2     "Sélectionner le chemin où %s devrai(en)t être installé.")
(set #WHAT_USER     "Partie utilisateur")
(set #WHAT_DEVELOP  "Partie développeur")
(set #WHAT_CRYPT    "Bibliothèques de cryptage")
(set #WHAT_SOURCE   "Sources et exemples")
(set #DEV_HOTHELP   "Fichiers HotHelp")
(set #DEV_FD	    "Fichiers FD")
(set #DEV_ASM	    "Includes ASM")
(set #DEV_C	    "Includes C/C++")
(set #DEVT_AUTODOCS "les autodocs")
(set #DEVT_FD_FILES "les fichiers fd")
(set #DEVT_ASM      "les includes ASM")
(set #DEVT_C	    "les includes C/C++")
(set #DEVT_HOTHELP  "les fichiers HotHelp")
(set #CRP_SOURCES   "les sources")
(set #SRC_SRC_A_EXA "les sources et exemples")
(set #DEVC_PRAGMA   "les fichiers pragma")
(set #DEVC_GCC      "les fichiers inline GCC")
(set #DEVC_REDIRECT "les fichiers de redirection pour le code plus ancien")
(set #DEVC_SAS      "les pragmas de redirection SAS (pas nécessaire)")
(set #DEVC_PROTO    "fichiers proto")
))

; ************************** Croatian texts ******************************

(if (= #catalog 5) (
(set #GOINGIN
  (cat "\n\nDobrodo¹li u XpkMaster distribucijski instalacijski sistem.\n\n"
       "Sve podarhive koje ¾elite instalirati, moraju biti odkompresirane u "
       "jedan imenik. Taj imenik je roditeljski od onoga gdje ste pokrenuli "
       "ovu skriptu!\n\nBudite sigurni da ste uèinili tako!"
  )
)
(set #INSTALLERVERSION "Trebate minimalno verziju 42.9 Instalera.")
(set #XPKMASTER_LIB  "Unaprijedi xpkmaster.library kad zatreba.")
(set #INSTALL_LIB    "Unaprijedi pod library %s.")
(set #WHAT_STUFF     "Koje programe da instaliram?")
(set #WHAT_LANGUAGE  "Koje jezike da instaliram?")
(set #WHAT_PROMPT    "Koje dijelove paketa ¾elite instalirati?")
(set #WHAT_LIBS      "Koje pod libraries ¾elite instalirati?")
(set #WHAT_DOCS      "\n\n\n\n\nInstalirati dokumentaciju?")
(set #WHAT_PREFS     "\n\n\n\n\nInstalirati Prefs program?")
(set #WHAT_PROCESSOR "\n\n\n\n\nInstalirati MC68020 verzije nekih librarija?")
(set #WHAT_DEVEL     "Koji dio developerskog imenikatrebate?")
(set #WHAT_C
  (cat "Izaberite dijelove koje trebate za svoj C kompajler. Datoteke budu "
        "automatski kopirane u ispravne imenike i podimenike."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\nInstalirati za¹titne lib soursove?")
(set #STARTUP        "\n\n\n\n\nDodaj XpkMasterPrefs poziv u startup datoteku.")
(set #PREFSFILE      "\n\n\n\n\nInstalirati  datoteku s parametrima?")
(set #ASK_PATH
  (cat "Izaberi imenik gdje %s bi trebao biti instaliran. Imenik "
       "»%s« æe automatski biti stvoren,ako ne postoji."
  )
)
(set #ASK_PATH2     "Izaber imenik gdje æe %s biti instaliran.")
(set #WHAT_USER     "Korisnièki dio")
(set #WHAT_DEVELOP  "Developerski dio")
(set #WHAT_CRYPT    "Za¹titni librariji")
(set #WHAT_SOURCE   "Izvorni kodovi i primjeri")
(set #DEV_AUTODOCS  "Autodocs")
(set #DEV_HOTHELP   "HotHelp datoteke")
(set #DEV_FD        "FD datoteke")
(set #DEV_ASM       "ASM dodaci")
(set #DEV_C         "C/C++ dodaci")
(set #DEVT_AUTODOCS "autodokumenti")
(set #DEVT_FD_FILES "FD datoteke")
(set #DEVT_ASM      "ASM dodaci")
(set #DEVT_C        "C/C++ dodaci")
(set #DEVT_HOTHELP  "HotHelp datoteke")
(set #CRP_SOURCES   "Izvorni kodovi")
(set #SRC_SRC_A_EXA "Primjeri i izvorni kodovi")
(set #DEVC_PRAGMA   "pragma datoteke")
(set #DEVC_GCC      "GCC linijske datoteke")
(set #DEVC_REDIRECT "preusmjeri datoteke za starije kodiranje")
(set #DEVC_SAS      "SAS pragmas preusmjeravanje (nije potrebno)")
(set #DEVC_PROTO    "proto datoteke")
))

; ************************** Italiano texts *****************************

(if (= #catalog 6) (
(set #GOINGIN
  (cat "\n\nBenvenuto nel sistema di installazione della distribuzione XpkMaster.\n\n"
       "Tutti i sotto archivi che desideri installare devono essere decompressi in"
       "una cartella. Questa cartella è la precedente a quella da cui tu hai fatto"
       "partire questo script!\n\nAssicurati di averlo fatto!"
  )
)
(set #INSTALLERVERSION "Necessiti come minimo della versione 42.9 dell' Installer.")
(set #XPKMASTER_LIB  "Aggiorna l' xpkmaster.library quando necessario.")
(set #INSTALL_LIB    "Aggiorna la sotto libreria %s.")
(set #WHAT_STUFF     "Che programmi desideri installare ?")
(set #WHAT_LANGUAGE  "Che file di linguaggio desideri installare?")
(set #WHAT_PROMPT    "Che parti della distribuzione desideri installare?")
(set #WHAT_LIBS      "Che sotto-librerie desideri installare?")
(set #WHAT_DOCS      "\n\n\n\n\nInstallo la documentazione?")
(set #WHAT_PREFS     "\n\n\n\n\nInstallo il programma di Preferenze ?")
(set #WHAT_PROCESSOR "\n\n\n\n\nInstallo le versioni MC68020 di alcune librerie?")
(set #WHAT_DEVEL     "Di che parti della cartella per sviluppatore hai bisogno?")
(set #WHAT_C	     "Scegli le parti che ti servono per il tuo compilatore C.")
(set #CRYPT_SOURCE   "\n\n\n\n\nInstallo i sorgenti della libreria di crittografia?")
(set #STARTUP	     "\n\n\n\n\nAggiungo la chiamata a XpkMasterPrefs nel file di startup.")
(set #ASK_PATH
  (cat "Seleziona il percorso in cui %s dovra' essere installato. Un cassetto "
       "»%s« verra' creato automaticamente, se non esiste gia'."
  )
)
(set #ASK_PATH2     "Seleziona il percorso in cui %s dovra' essere installato.")
(set #WHAT_USER     "Parte Utente")
(set #WHAT_DEVELOP  "Parte Sviluppatore")
(set #WHAT_CRYPT    "Librerie di crittografia")
(set #WHAT_SOURCE   "sorgenti ed esempi")
(set #DEV_HOTHELP   "File di Aiuto in linea")
(set #DEVT_AUTODOCS "gli autodocs")
(set #DEVT_FD_FILES "i file fd")
(set #DEVT_ASM      "gli includes ASM")
(set #DEVT_C	    "gli includes C/C++")
(set #DEVT_HOTHELP  "I file di Aiuto in linea")
(set #CRP_SOURCES   "I sorgenti")
(set #SRC_SRC_A_EXA "i sorgenti e gli esempi")
(set #DEVC_PRAGMA   "I file pragma")
(set #DEVC_REDIRECT "redirigo i file per il vecchio codice")
(set #DEVC_SAS      "redirigo i SAS pragmas (non necessario)")
))

; **************************** Dutch  texts *****************************
(if (= #catalog 7) (
(set #GOINGIN
  (cat "\n\nWelkom bij het XpkMaster distributie installatie systeem.\n\n"
       "Alle andere archieven die u wilt installeren moeten uitgepakt "
       "staan in een lade. Deze lade is de hogere dan de lade waaruit "
       "u dit script hebt gestart!\n\nVerzeker u ervan dat u dit gedaan "
       "hebt!"
  )
)
(set #INSTALLERVERSION "U bent tenminste versie 42.9 van Installer nodig.")
(set #XPKMASTER_LIB  "Werk xpkmaster.library bij, indien nodig.")
(set #INSTALL_LIB    "Werk sub library %s bij.")
(set #WHAT_STUFF     "Welke hulpprogramma's wilt u installeren?")
(set #WHAT_LANGUAGE  "Welke talen wilt u installeren?")
(set #WHAT_PROMPT    "Welke delen van de distributie wilt u installeren?")
(set #WHAT_LIBS	     "Welke sub library's wilt u installeren?")
(set #WHAT_DOCS	     "\n\n\n\n\nDocumentatie installeren?")
(set #WHAT_PREFS     "\n\n\n\n\nVoorkeuze programma installeren?")
(set #WHAT_PROCESSOR "\n\n\n\n\nMC68020-versies van een aantal library's installeren?")
(set #WHAT_DEVEL     "Welke delen van de ontwikkelaars-lade bent u nodig?")
(set #WHAT_C
  (cat "Kies de delen die u nodig bent voor uw C compiler. Deze bestanden "
       "worden automatisch in de juiste lades gezet."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\nCoderings library's installeren?")
(set #STARTUP	     "\n\n\n\n\nVoeg XpkMasterPrefs aanroep toe aan opstartbestand.")
(set #PREFSFILE	     "\n\n\n\n\nVoorkeuze-bestand installeren?")
(set #ASK_PATH
  (cat "Selecteer het pad waar %s geinstalleer moeten worden. Een lade "
       "%s wordt aangemaakt, indien deze niet bestaat."
  )
)
(set #ASK_PATH2	    "Selecteer het pad waar %s moet worden geinstalleerd.")
(set #WHAT_USER	    "Gebruikersgedeelte")
(set #WHAT_DEVELOP  "Ontwikkelaarsgedeelte")
(set #WHAT_CRYPT    "coderings library's")
(set #WHAT_SOURCE   "brondcodes en voorbeelden")
(set #DEV_AUTODOCS  "Autodocs")
(set #DEV_HOTHELP   "HotHelp bestanden")
(set #DEV_FD	    "FD bestanden")
(set #DEV_ASM	    "ASM includes")
(set #DEV_C 	    "C/C++ includes")
(set #DEVT_AUTODOCS "de autodocs")
(set #DEVT_FD_FILES "de FD bestanden")
(set #DEVT_ASM      "de ASM includes")
(set #DEVT_C	    "de C/C++ includes")
(set #DEVT_HOTHELP  "de HotHelp bestanden")
(set #CRP_SOURCES   "de broncodes")
(set #SRC_SRC_A_EXA "de broncodes en voorbeelden")
(set #DEVC_PRAGMA   "pragma bestanden")
(set #DEVC_GCC 	    "GCC inline bestanden")
(set #DEVC_REDIRECT "redirectie bestanden voor oudere code")
(set #DEVC_SAS 	    "SAS pragmas redirectie (niet noodzakelijk)")
(set #DEVC_PROTO    "proto bestanden")
))

; **************************** Polish texts *****************************

(if (= #catalog 9) (
(set #GOINGIN
  (cat "\n\nWitaj w programie instalacyjnym pakietu XpkMaster.\n\n"
       "Wszystkie pod-archiwa, których zawartoôê bëdziesz chciaî zainstalowaê "
       "muszâ byê rozpakowane do jednego katalogu. Katalogiem tym powinien "
       "byê katalog nadrzëdny do tego z którego uruchomiîeô ten skrypt instalacyjny!\n\n"
       "Upewnij sië, ûe tak to wîaônie zrobiîeô!"
  )
)
(set #INSTALLERVERSION "Ten skrypt wymaga Installlera w wersji 42.9 lub nowszego.")
(set #XPKMASTER_LIB  "Zaktualizuj xpkmaster.library w razie potrzeby.")
(set #INSTALL_LIB    "Zaktualizuj pob-bibliotekë %s.")
(set #WHAT_STUFF     "Które programy chcesz zainstalowaê?")
(set #WHAT_LANGUAGE  "Które wersje jëzykowe chcesz zainstalowaê?")
(set #WHAT_PROMPT    "Które czëôci dystrybucji chcesz zainstalowaê?")
(set #WHAT_LIBS      "Które pod-biblioteki chcesz zainstalowaê?")
(set #WHAT_DOCS      "\n\n\n\n\nCzy chcesz zainstalowaê dokumentacjë?")
(set #WHAT_PREFS     "\n\n\n\n\nCzy chcesz zainstalowaê panel preferencji?")
(set #WHAT_PROCESSOR "\n\n\n\n\nCzy chcesz zainstalowaê biblioteki w wersji dla procesora MC68020 lub nowszych?")
(set #WHAT_DEVEL     "Które czëôci z pakietu dla programistów bëdâ Ci potrzebne?")
(set #WHAT_C	     "Wybierz te elementy, które bëdziesz potrzebowaî przy pracy z Twoim kompilatorem C.")
(set #CRYPT_SOURCE   "\n\n\n\n\nCzy chcesz zainstalowaê tekst úródîowy biblioteki szyfrujâcej?")
(set #STARTUP	     "\n\n\n\n\nDodaje wywoîanie programu XpkMasterPrefs do sekwencji startowej.")
(set #ASK_PATH
  (cat "Wskaû, gdzie chcesz zainstalowaê %s. Katalog "
       "»%s« zostanie zaîoûony automatycznie, jeôli jeszcze nie istnieje."
  )
)
(set #ASK_PATH2     "Wskaû, gdzie chcesz zainstalowaê %s.")
(set #WHAT_USER     "Pliki dla uûytkownika")
(set #WHAT_DEVELOP  "Pliki dla programisty")
(set #WHAT_CRYPT    "biblioteki szyfrujâce")
(set #WHAT_SOURCE   "teksty úródîowe i przykîady")
(set #DEV_AUTODOCS  "dokumentacjë (Autodocs)")
(set #DEV_HOTHELP   "pliki dla systemu HotHelp")
(set #DEV_FD	    "pliki FD")
(set #DEV_ASM	    "inkludy dla asemblera")
(set #DEV_C	    "inkludy dla C/C++")
(set #DEVT_AUTODOCS "dokumentacjë (AutoDocs)")
(set #DEVT_FD_FILES "pliki FD")
(set #DEVT_ASM      "inkludy dla asemblera")
(set #DEVT_C	    "inkludy dla C/C++")
(set #DEVT_HOTHELP  "pliki dla systemu HotHelp")
(set #CRP_SOURCES   "teksty úródîowe")
(set #SRC_SRC_A_EXA "teksty úródîowe i przykîady")
(set #DEVC_PRAGMA   "pliki pragma")
(set #DEVC_GCC      "pliki inline dla GCC")
(set #DEVC_REDIRECT "pliki poôrednie (redirect) dla starszych úródeî")
(set #DEVC_SAS      "pliki poôrednie dla pragm kompilatora SAS (nie sâ potrzebne)")
(set #DEVC_PROTO    "pliki proto")
))

; **************************** Russian texts ****************************

(if (= #catalog 11) (
(set #GOINGIN
  (cat "\n\nÃíàïí îíåaêíáañû á çìðñaêêþñíï ãçðñïçàòñçáìíâí îaéäña XpkMaster.\n\n"
       "Áðä ðòà-aïôçáú, ìäíàôíãçëúä Áaë ãêþ çìðñaêêþõçç, ìa ìaöaêí çìðñaêêþõçç "
       "òåä ãíêåìú àúñû ïaðîaéíáaìú á íãçì éañaêíâ. Üñíñ éañaêíâ þáêþäñðþ îïäãúãòùçë "
       "éañaêíâíë íñ ñíâí, çæ éíñíïíâú Áú æaîòðñçêç çìðñaêêþñíï.\n\nÒàäãçñäðû á üñíë!"
  )
)
(set #INSTALLERVERSION "Ìäíàôíãçë Çìðñaêêþñíï éaé ëçìçëòë áäïðçç 42.9.")
(set #XPKMASTER_LIB  "Íàìíáêäìçä xpkmaster.library îïç ìäíàôíãçëíðñç.")
(set #INSTALL_LIB    "Íàìíáêäìçä àçàêçíñäéç îíããäïåéç %s.")
(set #WHAT_STUFF     "Éaéçä îïíâïaëëú Áú ôíñçñä îïíçìðñaêêçïíáañû?")
(set #WHAT_LANGUAGE  "Éaéçä þæúéíáúä óaèêú Áú ôíñçñä îïíçìðñaêêçïíáañû?")
(set #WHAT_PROMPT    "Éaéçä öaðñç îaéäña Áú ôíñçñä îïíçìðñaêêçïíáañû?")
(set #WHAT_LIBS      "Éaéçä àçàêçíñäéç îíããäïåéç Áú ôíñçñä îïíçìðñaêêçïíáañû?")
(set #WHAT_DOCS      "\n\n\n\n\nÇìðñaêêçïíáañû ãíéòëäìñaõçý?")
(set #WHAT_PREFS     "\n\n\n\n\nÇìðñaêêçïíáañû îïíâïaëëò Îïäãòðñaìíáíé?")
(set #WHAT_PROCESSOR "\n\n\n\n\nÇìðñaêêçïíáañû ìäéíñíïúä àçàêçíñäéç îíã MC68020?")
(set #WHAT_DEVEL     "Éaéçä öaðñç ãíéòëäìñaõçç ãêþ îïíâïaëëçðña Áaë ìòåìú?")
(set #WHAT_C
  (cat "Òéaåçñä öaðñç, ìäíàôíãçëúä Áaøäëò éíëîçêþñíïò Ðç. Óaèêú aáñíëañçöäðéç "
        "àòãòñ ïaæëäùäìú á ìòåìúô éañaêíâaô."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\nÇìðñaêêçïíáañû çðôíãìçéç øçóïòýùäè àçàêçíñäéç?")
(set #STARTUP      "\n\n\n\n\nÃíàaáêþý áúæíá XpkMasterPrefs á startup-óaèêä.")
(set #PREFSFILE      "\n\n\n\n\nÇìðñaêêçïíáañû óaèê îïäãòðñaìíáíé?")
(set #ASK_PATH
  (cat "Òéaåçñä ëaïøïòñ ãêþ çìðñaêêþõçç %s. Îïç íñðòñðñáçç "
       "éañaêíâa »%s«, íì àòãäñ ðíæãaì aáñíëañçöäðéç."
  )
)
(set #ASK_PATH2     "Òéaåçñä ëaïøïòñ ãêþ çìðñaêêþõçç %s.")
(set #WHAT_USER     "Ëaïøïòñ îíêûæíáañäêþ")
(set #WHAT_DEVELOP  "Ëaïøïòñ îïíâïaëëçðña")
(set #WHAT_CRYPT    "àçàêçíñäéç øçóïíáéç")
(set #WHAT_SOURCE   "çðôíãìçéç ç îïçëäïú")
(set #DEV_AUTODOCS  "Óaèêú Autodocs")
(set #DEV_HOTHELP   "Óaèêú HotHelp")
(set #DEV_FD        "Óaèêú FD")
(set #DEV_ASM       "ASM includes")
(set #DEV_C         "C/C++ includes")
(set #DEVT_AUTODOCS "óaèêú autodocs")
(set #DEVT_FD_FILES "óaèêú FD")
(set #DEVT_ASM      "óaèêú ASM includes")
(set #DEVT_C        "óaèêú C/C++ includes")
(set #DEVT_HOTHELP  "óaèêú HotHelp")
(set #CRP_SOURCES   "çðôíãìçéç")
(set #SRC_SRC_A_EXA "çðôíãìçéç ç îïçëäïú")
(set #DEVC_PRAGMA   "óaèêú pragma")
(set #DEVC_GCC      "óaèêú GCC inline")
(set #DEVC_REDIRECT "îäïäìaîïaáêþýùçä óaèêú ãêþ ðñaïúô éíãíá")
(set #DEVC_SAS      "Îïaâëú îäïäìaîïaáêäìçþ SAS (áíæëíåìí ìä ìòåìú)")
(set #DEVC_PROTO    "Óaèêú proto")
))

; ************************** Serbian texts ******************************

(if (= #catalog 12) (
(set #GOINGIN
  (cat "\n\nDobrodoãli u instalacioni sistem XpkMaster paketa.\n\n"
       "Sve dodatne arhive treba da se raspakuju u jedan direktorijum. "
       "To je direkorijum prethodan onome iz koga se starovali "
       "instalaciju!\n\n"
       "Obavezno uradite to!"
  )
)
(set #INSTALLERVERSION "Treba Vam najmanje verzija 42.9 Instalera.")
(set #XPKMASTER_LIB  "Osveäi xpkmaster.library kada je porebno.")
(set #INSTALL_LIB    "Osveäi podbiblioteke %s.")
(set #WHAT_STUFF     "Koje programe äelite da instalirate?")
(set #WHAT_LANGUAGE  "Koje jeziàke fajlove äelite da instalirate?")
(set #WHAT_PROMPT    "Koje delove paketa äelite da instalirate?")
(set #WHAT_LIBS      "Koje pod biblioteke äelite da instalirate?")
(set #WHAT_DOCS      "\n\n\n\n\nDa li da instaliram dokumentaciju?")
(set #WHAT_PREFS     "\n\n\n\n\nDa li da instaliram program za podeãavanja?")
(set #WHAT_PROCESSOR "\n\n\n\n\nDa li da instaliram MC68020 verzije nekih biblioteka?")
(set #WHAT_DEVEL     "Koji delovi direktorijuma za programere Vam trebaju?")
(set #WHAT_C	     "Odaberite delove koji Vam trebaju za C kompajler.")
(set #CRYPT_SOURCE   "\n\n\n\n\nDa li da instaliram izvorni kod biblioteka za ãifrovanje?")
(set #STARTUP	     "\n\n\n\n\nDa li da dodam poziv XpkMasterPrefs-u u startap.")
(set #ASK_PATH
  (cat "Odaberite putanju gde %s treba da se instalira. Fioka "
       "»%s« áe automatski biti napravljena, ako ne postoji."
  )
)
(set #ASK_PATH2     "Odaberite putanju gde %s treba da se instalira.")
(set #WHAT_USER     "Korisniàki deo")
(set #WHAT_DEVELOP  "Programerski deo")
(set #WHAT_CRYPT    "biblioteke za ãifrovanje")
(set #WHAT_SOURCE   "izvorni kod i primeri")
(set #DEV_AUTODOCS  "Autodokovi")
(set #DEV_HOTHELP   "HotHelp fajlovi")
(set #DEV_FD	    "FD fajlovi")
(set #DEV_ASM	    "ASM include-ovi")
(set #DEV_C	    "C/C++ include-ovi")
(set #DEVT_AUTODOCS "autodokove")
(set #DEVT_FD_FILES "fd fajlove")
(set #DEVT_ASM      "ASM include-ove")
(set #DEVT_C	    "C/C++ include-ove")
(set #DEVT_HOTHELP  "HotHelp fajlove")
(set #CRP_SOURCES   "izvorni kod")
(set #SRC_SRC_A_EXA "izvorni kod i primere")
(set #DEVC_PRAGMA   "pragma fajlove")
(set #DEVC_GCC      "GCC inline fajlove")
(set #DEVC_REDIRECT "redirekt fajlove za stariji kod")
(set #DEVC_SAS      "SAS pragma redirekt (nije potrebno)")
(set #DEVC_PROTO    "proto fajlove")
))

; ************************** Finnish texts ******************************

(if (= #catalog 13) (
(set #GOINGIN
  (cat "\n\nTervetuloa asentamaan XpkMaster järjestelmääsi.\n\n"
       "Kaikki ohjelmistoon kuuluvat arkistot, jotka haluat asentaa, täytyy purkaa "
       "samaan hakemistoon. Kyseinen hakemisto on sen hakemiston edeltäjä, josta "
       "käynnistit tämän asennusohjelman!\n\nVarmista tehneesi näin!"
  )
)
(set #INSTALLERVERSION "Tarvitset vähintään version 42.9 Installer-ohjelmasta.")
(set #XPKMASTER_LIB  "Päivitä xpkmaster.library tarvittaessa.")
(set #INSTALL_LIB    "Päivitä alikirjasto %s.")
(set #WHAT_STUFF     "Mitkä ohjelmat haluaisit asentaa?")
(set #WHAT_LANGUAGE  "Mitkä kielitiedostot haluaisit asentaa?")
(set #WHAT_PROMPT    "Mitkä osat ohjelmistosta haluaisit asentaa?")
(set #WHAT_LIBS      "Mitkä alikirjastot haluaisit asentaa?")
(set #WHAT_DOCS      "\n\n\n\n\nAsennetaanko dokumentit?")
(set #WHAT_PREFS     "\n\n\n\n\nAsennetaanko asetusohjelma?")
(set #WHAT_PROCESSOR "\n\n\n\n\nAsennetaanko MC68020-versiot eräistä kirjastoista?")
(set #WHAT_DEVEL     "Mitä osia tarvitset ohjelmoijan materiaalista?")
(set #WHAT_C
  (cat "Valitse osat, joita tarvitset C-kääntäjääsi varten. Tiedostot kopioidaan "
        "automaattisesti oikeisiin alihakemistoihin."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\nAsennetaanko salauskirjastojen lähdekoodit?")
(set #STARTUP	     "\n\n\n\n\nLisää XpkMasterPrefs käynnistystiedostoon?")
(set #PREFSFILE	     "\n\n\n\n\nAsennetaanko asetustiedosto?")
(set #ASK_PATH
  (cat "Valitse, mihin %s tulisi asentaa. Hakemisto nimeltä "
       "»%s« luodaan automaattisesti, mikäli se ei ole olemassa."
  )
)
(set #ASK_PATH2     "Valitse, mihin %s tulisi asentaa.")
(set #WHAT_USER     "Käyttäjän materiaali")
(set #WHAT_DEVELOP  "Ohjelmoijan materiaali")
(set #WHAT_CRYPT    "salauskirjastot")
(set #WHAT_SOURCE   "lähdekoodit ja esimerkit")
(set #DEV_AUTODOCS  "Autodocit")
(set #DEV_HOTHELP   "HotHelp-tiedostot")
(set #DEV_FD	    "FD-tiedostot")
(set #DEV_ASM	    "ASM-includet")
(set #DEV_C	    "C/C++ -includet")
(set #DEVT_AUTODOCS "autodocit")
(set #DEVT_FD_FILES "FD-tiedostot")
(set #DEVT_ASM      "ASM-includet")
(set #DEVT_C	    "C/C++ -includet")
(set #DEVT_HOTHELP  "HotHelp-tiedostot")
(set #CRP_SOURCES   "lähdekoodit")
(set #SRC_SRC_A_EXA "lähdekoodit ja esimerkit")
(set #DEVC_PRAGMA   "pragma-tiedostot")
(set #DEVC_GCC      "GCC inline -tiedostot")
(set #DEVC_REDIRECT "uudelleenohjaustiedostot vanhalle koodille")
(set #DEVC_SAS      "SAS pragmojen uudelleenohjaus (ei tarvita)")
(set #DEVC_PROTO    "proto-tiedostot")
))

; **************************** Swedish texts ****************************

(if (= #catalog 14) (
(set #GOINGIN
  (cat "\n\nVälkommen till XpkMaster-distributionens installationssystem.\n\n"
       "Alla underarkiv du vill installera måste vara uppackade i "
       "en låda. Denna låda är moderlådan till den låda där du "
       "har startat detta skript!\n\nSe till att du har gjort det!"
  )
)
(set #INSTALLERVERSION "Du behöver åtminstone version 42.9 av Installer.")
(set #XPKMASTER_LIB  "Uppdatera xpkmaster.library om det behövs.")
(set #INSTALL_LIB    "Uppdatera underbiblioteket %s.")
(set #WHAT_STUFF     "Vilka program vill du installera?")
(set #WHAT_LANGUAGE  "Vilka språkfiler vill du installera?")
(set #WHAT_PROMPT    "Vilka delar av distributionen vill du installera?")
(set #WHAT_LIBS      "Vilka underbibliotek vill du installera?")
(set #WHAT_DOCS      "\n\n\n\n\nInstallera dokumentation?")
(set #WHAT_PREFS     "\n\n\n\n\nInstallera inställningsprogram?")
(set #WHAT_PROCESSOR "\n\n\n\n\nInstallera MC68020-versioner av några bibliotek?")
(set #WHAT_DEVEL     "Vilka delar av utvecklarlådan behöver du?")
(set #WHAT_C
  (cat "Välj de delar som du behöver för din C-kompilator. Filerna "
        "kopieras automatiskt till de rätta underlådorna."
  )
)
(set #CRYPT_SOURCE   "\n\n\n\n\nInstallera krypteringslib-källkod?")
(set #STARTUP     "\n\n\n\n\nLägg till XpkMasterPrefs till startfilen?")
(set #PREFSFILE     "\n\n\n\n\nInstallera inställningsfilen?")
(set #ASK_PATH
  (cat "Välj sökväg där %s ska installeras. En låda "
       "»%s« kommer att skapas automatiskt om den inte finns."
  )
)
(set #ASK_PATH2     "Välj sökväg där %s ska installeras.")
(set #WHAT_USER     "Användardel")
(set #WHAT_DEVELOP  "Utvecklardel")
(set #WHAT_CRYPT    "krypteringsbibliotek")
(set #WHAT_SOURCE   "källkoder och exempel")
(set #DEV_HOTHELP   "HotHelp-filer")
(set #DEV_FD        "FD-filer")
(set #DEV_ASM       "ASM include-filer")
(set #DEV_C         "C/C++ include-filer")
(set #DEVT_AUTODOCS "autodocs-filerna")
(set #DEVT_FD_FILES "FD-filerna")
(set #DEVT_ASM      "ASM include-filerna")
(set #DEVT_C        "C/C++ include-filerna")
(set #DEVT_HOTHELP  "HotHelp-filerna")
(set #CRP_SOURCES   "källkoderna")
(set #SRC_SRC_A_EXA "källkoderna och exempel")
(set #DEVC_PRAGMA   "pragma-filer")
(set #DEVC_GCC      "GCC inline-filer")
(set #DEVC_REDIRECT "omdirigeringsfiler för gammal kod")
(set #DEVC_SAS      "SAS pragmas omdirigering (ej nödvändig)")
(set #DEVC_PROTO    "proto-filer")
))

; ***************************** Czech texts *****************************

(if (= #catalog 15) (
(set #GOINGIN
  (cat "\n\nVÉtejte pÒi instalaci syst×mu XpkMaster.\n\n"
       "VÓechny podpÊrn× archÉvy, kter× chcete nainstalovat musÉ bÙt "
       "rozbaleny do jednoho adresÁÒe. Tento adresÁÒ je o jeden vÙÓe "
       "neÚ ten, ze kter×ho jste spustili instalaÃnÉ skript!\n\n"
       "UjistÅte se, Úe jste tak uÃinili!"
  )
)
(set #INSTALLERVERSION "Je vyÚadovÁn Installer alespoÎ ve verzi 42.9.")
(set #XPKMASTER_LIB    "NovÁ verze knihovny xpkmaster.library.")
(set #INSTALL_LIB      "NovÁ verze podpÊrn× knihovny %s.")
(set #WHAT_STUFF       "Kter× programy si pÒejete nainstalovat?")
(set #WHAT_LANGUAGE    "Kter× lokalizaÃnÉ katalogy si pÒejete nainstalovat?")
(set #WHAT_PROMPT      "Kter× ÃÁsti syst×mu Xpk si pÒejete nainstalovat?")
(set #WHAT_LIBS        "Kter× podpÊrn× knihovny si pÒejete nainstalovat?")
(set #WHAT_DOCS        "\n\n\n\n\nInstalovat dokumentaci?")
(set #WHAT_PREFS       "\n\n\n\n\nInstalovat NastavenÉ pro Xpk?")
(set #WHAT_PROCESSOR   "\n\n\n\n\nInstalovat verze knihoven pro MC68020?")
(set #WHAT_DEVEL       "Kter× ÃÁsti adresÁÒe pro vÙvojÁÒe si pÒejete nainstalovat?")
(set #WHAT_C	        "Volba souborÊ pro VÁÓ kompilÁtor C.")
(set #CRYPT_SOURCE     "\n\n\n\n\nInstalovat zdrojov× texty ÓifrovacÉch knihoven?")
(set #STARTUP	        "\n\n\n\n\nSpouÓtÅt XpkMasterPrefs automaticky pÒi startu.")
(set #ASK_PATH
  (cat "Volba cesty, kde by mÅl bÙt %s nainstalovÁn. AdresÁÒ "
       "»%s« bude automaticky vytvoÒen, pokud neexistuje."
  )
)
(set #ASK_PATH2     "Volba cesty kde by mÅl bÙt %s nainstalovÁn.")
(set #WHAT_USER     "ãÁst pro uÚivatele")
(set #WHAT_DEVELOP  "ãÁst pro vÙvojÁÒe")
(set #WHAT_CRYPT    "ÓifrovacÉ knihovny")
(set #WHAT_SOURCE   "zdrojov× texty a pÒÉklady")
))

; ************************* global procedures ***************************

(procedure P_copylib lib pathadd path
  (
    (set #libr (cat "xpk" lib ".library"))
    (copylib (prompt (#INSTALL_LIB #libr))
      (help @copylib-help)
      (source (cat path "Libs" pathadd "/compressors/" #libr))
      (dest "LIBS:compressors")
      (confirm)
    )
  )
)
  
; ********************* xpk_User archive install *************************

(procedure P_install_user
  (
    (copylib (prompt #XPKMASTER_LIB)
      (help @copylib-help)
      (source (cat (if (< #OS_version 37) "Libs_1.3/" "Libs/") "xpkmaster.library"))
      (dest "LIBS:")
      (confirm)
    )

    (set #p
      (askoptions
        (prompt #WHAT_LIBS)
        (help @askoptions-help)
        (choices "CBR0" "DLTA" "DUKE" "FAST" "FRLE" "HUFF" "IMPL" "NONE"
          "NUKE" "RLEN" "SHRI" "SMPL" "HFMN" "MASH" "RAKE" "SQSH")
        (default 4294967295)
      )
    )

    (makedir "LIBS:compressors")

    (set #q (if (IN #p 12 13 14 15)
      (askbool
        (prompt #WHAT_PROCESSOR)
        (help @askbool-help)
        (default 0)
      ))
    )

    (set #n 0)
    (while (set #thisfile (select #n "CBR0" "DLTA" "DUKE" "FAST" "FRLE"
      "HUFF" "IMPL" "NONE" "NUKE" "RLEN" "SHRI" "SMPL" ""))
      (
        (if (IN #p #n) (P_copylib #thisfile "" ""))
        (set #n (+ #n 1))
      )
    )

    (set #i 0)
    (while (set #thisfile (select #i "HFMN" "MASH" "RAKE" "SQSH" ""))
      (
        (if (IN #p (+ #n #i))
          (P_copylib #thisfile (if #q "_68020+" "") ""))
        (set #i (+ #i 1))
      )
    )

    (copyfiles
      (source "C/")
      (dest "C:")
      (prompt #WHAT_STUFF)
      (help @askoptions-help)
      (choices "xDir" "xQuery" "xType" (if (>= #OS_version 37) "xPack" "xPK")
      (if (>= #OS_version 37) "xBench") "xLoadSeg" "xScan" "xUP")
      (confirm)
    )

    (set #n (if (>= #OS_version 37)
      (askbool
        (prompt #WHAT_PREFS)
        (help @askbool-help)
        (default 0)
      )
    ))

    (if #n (
      (copyfiles
        (source "Prefs/")
        (dest "SYS:Prefs")
        (choices "XpkMaster")
        (infos)
      )
      (copyfiles (source "C/") (dest "C:") (choices "XpkMasterPrefs"))
      (startup "XpkMasterPrefs" (prompt #STARTUP) (help @startup-help)
        (command "Run >NIL: XpkMasterPrefs"))
      (copyfiles (source "EnvArc/") (dest "ENVARC:") (prompt #PREFSFILE)
      (help @copylib-help) (choices "xpkmaster.prefs") (confirm))
    ))

    (if (>= #OS_version 37)(
      (set #p
        (askoptions
          (prompt #WHAT_LANGUAGE)
          (help @askoptions-help)
          (choices
		"català" "dansk" "deutsch" "español" "français" "hrvatski"
		"italiano" "nederlands" "norsk" "polski" "português"
		"russian" "srpski" "suomi" "Svenska" "ÃeÓtina"
	  )
          (default (shiftleft 1 #catalog))
        )
      )

      (set #i 0)
      (while (set #thisfile (select #i
		"català" "dansk" "deutsch" "español" "français" "hrvatski"
		"italiano" "nederlands" "norsk" "polski" "português"
		"russian" "srpski" "suomi" "Svenska" "ÃeÓtina"
      ""))
      (
        (set #thisfile (tackon "catalogs/" #thisfile))
        (if (IN #p #i)(
          (copyfiles (source #thisfile)
            (dest (tackon "LOCALE:" #thisfile))
            (pattern "xpkmaster.catalog")
	  )
          (if #n (copyfiles (source #thisfile)
            (dest (tackon "LOCALE:" #thisfile))
            (pattern "xpkmasterprefs.catalog")
	  ))
        ))
        (set #i (+ #i 1))
      ))
    ))

    (copyfiles
      (source "")
      (dest @default-dest)
      (prompt #WHAT_STUFF)
      (help @askoptions-help)
      (choices "XpkMaster.guide")
      (infos)
      (noposition)
      (confirm)
    )
  )
)

; ********************* xpk_Develop archive install **********************

(procedure P_install_develop
  (
    (set #p
      (askoptions
        (prompt #WHAT_DEVEL)
        (help @askoptions-help)
        (choices #DEV_AUTODOCS #DEV_HOTHELP #DEV_FD #DEV_ASM #DEV_C)
        (default 4294967295)
      )
    )

    (set #dir (if(exists "INCLUDE:" (NOREQ)) "INCLUDE:" @default-dest))

    (if (IN #p 0)
      (
        (set #dir (askdir (prompt (#ASK_PATH2 #DEVT_AUTODOCS)) (help @askdir-help) (default #dir)))
        (copyfiles (source (tackon #xpk_Develop "AutoDocs/")) (dest #dir) (all))
      )
    )
    (if (IN #p 2)
      (
        (set #dir (askdir (prompt (#ASK_PATH2 #DEVT_FD_FILES)) (help @askdir-help) (default #dir)))
        (copyfiles (source (tackon #xpk_Develop "Include/FD/")) (dest #dir) (all))
      )
    )
    (if (IN #p 3)
      (
        (set #dir (askdir (prompt (#ASK_PATH2 #DEVT_ASM)) (help @askdir-help) (default #dir)))
        (copyfiles (source (tackon #xpk_Develop "Include/ASM/")) (dest #dir) (all))
      )
    )
    (if (IN #p 4)
      (
        (set #q
          (askoptions
            (prompt #WHAT_DEVEL)
            (help @askoptions-help)
            (choices #DEVC_PRAGMA #DEVC_PROTO #DEVC_GCC #DEVC_REDIRECT
            #DEVC_SAS)
            (default 3)
          )
        )
        (set #dir (askdir (prompt (#ASK_PATH2 #DEVT_C)) (help @askdir-help) (default #dir)))
        (copyfiles (source (tackon #xpk_Develop "Include/C/clib/")) (dest (tackon #dir "clib")) (all))
        (copyfiles (source (tackon #xpk_Develop "Include/C/xpk/")) (dest (tackon #dir "xpk")) (all))
	(if (IN #q 0)
	  (copyfiles (source (tackon #xpk_Develop "Include/C/pragma/")) (dest (tackon #dir "pragma")) (all))
	)
	(if (IN #q 1)
	  (copyfiles (source (tackon #xpk_Develop "Include/C/proto/")) (dest (tackon #dir "proto")) (all))
	)
	(if (IN #q 2)
	  (copyfiles (source (tackon #xpk_Develop "Include/C/inline/")) (dest (tackon #dir "inline")) (all))
	)
	(if (IN #q 3)
	  (copyfiles (source (tackon #xpk_Develop "Include/Old-C/libraries/")) (dest (tackon #dir "libraries")) (all))
	)
	(if (IN #q 4)
	  (copyfiles (source (tackon #xpk_Develop "Include/OLD-C/pragmas/")) (dest (tackon #dir "pragmas")) (all))
	)
      )
    )
    (if (IN #p 1)
      (
        (set #dir (askdir (prompt (#ASK_PATH2 #DEVT_HOTHELP)) (help @askdir-help) (default (if(exists "HOTHELP:" (NOREQ)) "HOTHELP:Projekte" @default-dest))))
        (copyfiles (source (tackon #xpk_Develop "HotHelp/")) (dest #dir) (pattern "XPK#?"))
      )
    )
  )
)

; ********************* xpk_Crypt archive install ************************

(procedure P_install_crypt
  (
    (set #i
      (askoptions
        (prompt #WHAT_LIBS)
        (help @askoptions-help)
        (choices "ENCO" "FEAL" "IDEA")
        (default 4294967295)
      )
    )

    (set #n 0)
    (while (set #thisfile (select #n "ENCO" "FEAL" "IDEA" ""))
      (
        (if (IN #i #n) (P_copylib #thisfile "" #xpk_Crypt))
        (set #n (+ #n 1))
      )
    )

    (if (askbool (prompt #CRYPT_SOURCE) (help @askbool-help) (default 0))
      (
        (set #dir
          (tackon
            (askdir (prompt (#ASK_PATH #CRP_SOURCES "XpkSources/Crypt")) (help @askdir-help) (default @default-dest))
            "XpkSources/Crypt"))
        (if (NOT (exists #dir)) (makedir #dir (infos)))
        (copyfiles (source (tackon #xpk_Crypt "source/")) (dest #dir) (all))
      )
    )
  )
)

; ********************* xpk_Source archive install ***********************

(procedure P_install_source
  (
    (set #dir
      (tackon
        (askdir (prompt (#ASK_PATH #SRC_SRC_A_EXA "XpkSources")) (help @askdir-help) (default @default-dest))
           "XpkSources"))
        (if (NOT (exists #dir)) (makedir #dir (infos)))
        (copyfiles (source #xpk_Source) (dest #dir) (all))
  )
)

; ******************** here the stuff really starts **********************

(if (< @installer-version (+ (* 65536 42) 9))
  (abort #INSTALLERVERSION)
)
; we need at least version 42.9 of Installer, as some of it's functionality
; is used in the script

(set #xpk_Source  "/xpk_Source/")
(set #xpk_Develop "/xpk_Develop/")
(set #xpk_Crypt   "/xpk_Crypt/")
(set #OS_version  (/ (getversion) 65536))

(message #GOINGIN)
; may be here comes an selection, where the decrunched archives are

(set #o
  (askoptions
    (prompt #WHAT_PROMPT)
    (help @askoptions-help)
    (choices #WHAT_USER
      (if (exists #xpk_Develop) #WHAT_DEVELOP "")
      (if (exists #xpk_Crypt)   #WHAT_CRYPT   "")
      (if (exists #xpk_Source)  #WHAT_SOURCE  "")
    )
    (default 1)
  )
)

(if (IN #o 0) (P_install_user))
(if (IN #o 1) (P_install_develop))
(if (IN #o 2) (P_install_crypt))
(if (IN #o 3) (P_install_source))

